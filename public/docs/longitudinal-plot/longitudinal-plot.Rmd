---
title: "Longitudinal plot (SAS)"
output: html_document
author: Ting Wu
date: '2021-03-12'
categories:
  - SAS
tags:
  - Repeated measurements

---
###Longitudinal plot
About the data example: The aortic root sizes were measured over time for two groups of patients. The change of root size over time is of interest. In this analysis, we first plotted the group mean at each discrete time to visualize the root growth, secondly modeled the continuous time effect of root growth. The data is setup in a long format that each subject with ID (sBAV_ID_Number) has multiple rows with multiple measurements at different times. 

1) To generate longitudinal plots:

```{r, out.width = "400px", eval=FALSE}

*plot the group mean over time with formatting; 

goptions reset=all ftext=swiss ;
axis1 label=(h=2 a=90 "Root size (mm)") value=( h=2 );
axis2 label= (h=2 "Time (year)") value=( h=2 );
legend1 label=(f=swiss h=2  'Root Group')  value=(h=2 color=black  'root control' 'root dilation' ) ;

* time2 is a decrete time variable in the dataset; 
proc sort data=rootdat out=rootdat_t nodupkey; by sBAV_ID_Number time2;run;  

proc gplot data=rootdat_t;
plot root_size*time2=Root_group / haxis=axis2 vaxis=axis1 legend = legend1 ;
symbol1 c=red i= std1mjt  l=1 w=2 mode=include r=1;
symbol2 c=blue i= std1mjt  l=2  w=2 mode=include r=1;
title h=3 "Average root size by group with standard errors of means";
run;
quit;

```


2) Output the plot
```{r, out.width = "400px", eval=FALSE}

ods graphics on / reset=all imagefmt=tiff imagename="KM_Stented_vs_Stentless";
ods listing style=Statistical gpath="C:\xx";
ods graphics on / width=5in;
ods listing image_dpi=500;run;
```


3) To model the change of aorta root over time, time is modeled as a continuous variable in the model. The Class variable time is used in the repeated statement with compound symmetry as the covariance matrix to account for residual correlation within a subject. Mixed effect model is preferred when the longitudinal data is unbalanced. Repeated ANOVA is only used for balanced data. The interaction between group effect and time is tested first, if not significant, will be removed from the model. 

```{r, out.width = "400px", eval=FALSE}

**model; 
*timcl2 is class variable that is placed at the repeated statement to account for patient level (sBAV_ID) cluster; 
* time is modeled as a countinous variable. 

proc sort data=rootdat; by sBAV_ID_Number timecl;run;
data rootdat; set rootdat;  timecl2=time;run;

Proc mixed data=rootdat method=ml covtest empirical;
class sBAV_ID_Number  Root_group (ref='0') timecl2;
model root_size = time Root_group time*Root_group/ solution;
repeated timecl2 / type =CS subject = sBAV_ID_Number;
run;

******if the interaction is not significant****;

Proc mixed data=rootdat method=ml covtest empirical;
class sBAV_ID_Number  Root_group (ref='0') timecl2;
model root_size = time Root_group / solution;
repeated timecl2 / type =CS subject = sBAV_ID_Number;
run;


******if there were an interaction****;
Proc mixed data=rootdat_t method=ml covtest empirical;
class sBAV_ID_Number  Root_group (ref='0') timecl ;
model root_size = time2 Root_group time2*Root_group / solution;
repeated  timecl/ type =CS subject = sBAV_ID_Number;
estimate "root group_diff at 5 y"  Root_group 1 -1
                             time2*Root_group   5 -5;

estimate "root group_diff at 10 y"  Root_group 1 -1
                            time2*Root_group   10 -10;

estimate "root group_diff at 15 y"  Root_group 1 -1
                             time2*Root_group   15 -15;

estimate "time effect for group 1" time2 1 0
                                   time2*Root_group   1 0;
estimate "time effect for group 0" time2 1 0
                                   time2*Root_group   0 1;
run;

                                   
```
